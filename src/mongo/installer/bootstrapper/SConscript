# -*- mode: python; -*-

import os

Import("env")
Import("get_option")
Import("has_option")

if not env.TargetOSIs('windows'):
    Return()

import re
import subprocess
import winreg

env = env.Clone()

if not 'VSINSTALLDIR' in env['MSVS']:
    print("SCons tool setup did not configure the path to Visual Studio, disabling WiX bootstrapper")
    Return()

# Get relative path from subfolders (BA / wxs) to build folder
buildAbsDir = env.Dir("$BUILD_DIR").abspath
srcAbsDir = Dir('.').srcnode().abspath
outDir = os.path.relpath(buildAbsDir, srcAbsDir)
outDir = "..\\" + outDir + "\\bootstrapper\\"
objDir = outDir + "obj\\"

serverMsi = buildAbsDir + "\\msi\\" + env['SERVER_DIST_BASENAME'] + ".msi"
serverExe = env['SERVER_DIST_BASENAME']

# major version is the x.y, not the x.y.z
full_version = env['MONGO_VERSION'].partition('-')[0]
major_version = full_version
mv = major_version.split('.')
major_version = "%s.%s" % (mv[0], mv[1])

env['ENV']['WIX'] = os.environ.get('WIX')
env['ENV']['MongoDBVersion'] = full_version
env['ENV']['MongoDBMajorVersion'] = major_version
env['ENV']['MongoMsi'] = serverMsi
# Force output paths in all MSBuild flavours
env['ENV']['OutputPath'] = outDir
env['ENV']['IntermediateOutputPath'] = objDir
env['ENV']['OutDir'] = outDir
env['ENV']['IntDir'] = objDir
env['ENV']['OutputDirectory'] = outDir
env['ENV']['IntermediateDirectory'] = objDir
env['ENV']['BundleOutputName'] = serverExe
env['ENV']['RunWixToolsOutOfProc'] = 'true'

sources = ["bootstrapper.sln"]
exe = "$BUILD_DIR/bootstrapper/${SERVER_DIST_BASENAME}.exe"
exe_cmd = env.Command(exe,
            sources,
            "\"${MSVS['VSINSTALLDIR']}" + '\Common7\IDE\devenv" ${SOURCES} /Rebuild "Release|x86"')

# Generated Dependencies
env.Depends(exe_cmd, "$BUILD_DIR/msi/${SERVER_DIST_BASENAME}.msi")
env.AlwaysBuild(exe)
env.NoCache(exe)

env.Alias( "bootstrapper" , exe_cmd )
